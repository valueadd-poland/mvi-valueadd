apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/scripts/versioning.gradle"
apply from: "$rootDir/scripts/jfrog.gradle"


// Android library configuration
android {
    compileSdkVersion deps.build_version.compile_sdk
    buildToolsVersion deps.build_version.tools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion deps.build_version.min_sdk
        targetSdkVersion deps.build_version.target_sdk
        versionName libraryVersionName
        versionCode libraryVersionCode
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

// Dependencies configuration
dependencies {
    // Application files
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    //AndroidX
    implementation deps.androidx.appcompat
    implementation deps.androidx.core_ktx
    implementation deps.androidx.material

    // Kotlin
    implementation deps.kotlin.stdlib_jdk8

    // Reactivex
    implementation deps.rx.java
    implementation deps.rx.android
    implementation deps.rx.kotlin
    implementation deps.rx.binding

    // Utility
    implementation deps.utility.fragmentation

    // Test dependencies
    testImplementation deps.test.mockk
    testImplementation deps.test.junit_api
    testImplementation deps.test.junit_params
    testRuntimeOnly deps.test.junit_engine
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    enabled = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
