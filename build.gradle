/**
 * Top-level build dependencies and configuration options common to all sub-projects/modules.
 */
buildscript {
    repositories {
        apply from: "dependencies.gradle"
        applyRepositories(repositories)
    }
    dependencies {
        classpath deps.build.android_gradle_plugin
        classpath deps.build.kotlin_gradle_plugin
        classpath deps.build.junit5_runner
        classpath deps.build.android_maven
        classpath deps.build.bintray
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version "1.0.0"
    id 'com.jfrog.bintray' version "1.8.4" apply false
    id 'maven-publish'
}

/**
 * Dependencies for all projects
 */
allprojects {
    repositories {
        applyRepositories(repositories)
    }
    apply from: "$rootDir/scripts/detekt.gradle"
    apply from: "$rootDir/scripts/ktlint.gradle"
    apply from: "$rootDir/scripts/findbugs.gradle"
    apply from: "$rootDir/scripts/pmd.gradle"
}

subprojects { Project project ->
    if (project.name.startsWith('mvi')) {
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'maven-publish'

        project.afterEvaluate {
            publishing {
                repositories {
                    maven {
                        def releasesRepoUrl = "https://dl.bintray.com/valueadd-poland/maven"
                        def snapshotsRepoUrl = "https://dl.bintray.com/valueadd-poland/snapshots"
                        url = islibraryVersionSnapshot
                                ? snapshotsRepoUrl
                                : releasesRepoUrl
                        println("Repository url is $url")
                    }
                }
            }
        }
    }
}