/**
 * Top-level build dependencies and configuration options common to all sub-projects/modules.
 */
buildscript {
    repositories {
        apply from: "dependencies.gradle"
        applyRepositories(repositories)
    }
    dependencies {
        classpath deps.build.android_gradle_plugin
        classpath deps.build.kotlin_gradle_plugin
        classpath deps.build.junit5_runner
        classpath deps.build.android_maven
        classpath deps.build.safeargs
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version "1.0.0"
    id 'maven-publish'
}

/**
 * Dependencies for all projects
 */
allprojects {
    repositories {
        applyRepositories(repositories)
    }
    apply from: "$rootDir/scripts/detekt.gradle"
    apply from: "$rootDir/scripts/ktlint.gradle"
    apply from: "$rootDir/scripts/pmd.gradle"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.3"
            useIR = findProperty("kotlin.ir.enabled")?.toBoolean() == true
        }
    }
}